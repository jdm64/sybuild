import std.cstring;
import std.cstdio;
import std.ctime;

void write(StrPtr str, @FILE stream)
{
	fwrite(str, 1, strlen(str), stream);
}

void readOutput(@FILE stream)
{
	[128]int8 buf;
	int n;
	while ((n = fread(buf$, 1, buf->size, stream)) > 0)
		fwrite(buf$, 1, n, stdout);
}

int runCmd(StrPtr cmd)
{
	auto pf = popen(cmd, "r");
	readOutput(pf);
	return pclose(pf);
}

class Builder
{
	struct this {
		cstring outfile, ofiles;
		bool rebuild;
	}

	this() outfile{"out.bin"}, ofiles{}, rebuild{false}
	{}

	int compileFile(StrPtr file)
	{
		int i;
		for (i = strlen(file) - 1; file[i] != '.'; i--)
		{}

		cstring ofile;
		ofile.addRange(file, i).add(".o");
		auto otime = getMTime(ofile.get());
		auto stime = getMTime(file);

		if (otime > stime)
			return 0;
		rebuild = true;

		cstring cmd{"saphyr "};
		cmd.add(file);

		return runCmd(cmd.get());
	}

	void addOFile(StrPtr file)
	{
		int i;
		for (i = strlen(file) - 1; file[i] != '.'; i--)
		{}

		ofiles.addRange(file, i).add(".o").add(" ");
	}

	int parse(int argc, @[]StrPtr argv)
	{
		for (int i = 1; i < argc; i++) {
			if (!strcmp("-o", argv[i]) && i + 1 < argc) {
				outfile = argv[++i];
				continue;
			}
			auto r = compileFile(argv[i]);
			if (r)
				return r;
			addOFile(argv[i]);
		}
		return 0;
	}

	int run()
	{
		if (!rebuild)
			return 0;

		cstring cmd{"clang "};
		cmd.add("-o ").add(outfile).add(" ").add(ofiles);

		return runCmd(cmd.get());
	}
}

int main(int argc, @[]StrPtr argv)
{
	if (argc < 2) {
		write("no arguments provided\n", stdout);
		return -1;
	}

	Builder builder;
	auto r = builder.parse(argc, argv);
	if (!r)
		r = builder.run();
	return r;
}
